spwnum = import spwnum
extract import std
extract $
extract obj_props
model = import "torus.spwn"

tensor = spwnum.tensor.tensor
vector = spwnum.vector.vector

mlen = model.length

// rendering function
Z_OFFSET = 10
K = 450

/*
Math:
K = z', where z' is a chosen constant

(x', y') = (Kx/z, Ky/z)
*/

render_vertices = (theta: @number) {
    transform = spwnum.tensor.graphics(rank = 3).ry(theta) * spwnum.tensor.graphics(rank = 3).rx(theta)
    let ret_arr = []
    
    for i in 0..mlen {
        new_v = transform * model[i]

        x = new_v.matrix[0][0]
        y = new_v.matrix[1][0]
        z = new_v.matrix[2][0] + Z_OFFSET

        x_p = K * x / z
        y_p = K * y / z

        ret_arr.push([x_p, y_p])
    }

    return ret_arr
}

projection_v = render_vertices(0)

// // objects
let block_groups = []
for i in ..mlen {
    block_groups.push(?g)
}

for i in ..mlen {
    add(obj {
        OBJ_ID: 1117,
        X: projection_v[i][0],
        Y: projection_v[i][1],
        GROUPS: [block_groups[i], 9g],
    })
}

for theta in 20..20..380 {
    let v_n = render_vertices(theta * 3.14 / 180)
    let v_n_1 = render_vertices((theta - 20) * 3.14 / 180)
    
    for i in ..mlen {
        let dx = v_n[i][0] - v_n_1[i][0]
        let dy = v_n[i][1] - v_n_1[i][1]
        dx /= 3
        dy /= 3

        ->block_groups[i].move(dx, dy, 2)
    }

    print("Rendered frame: " + (theta / 20) as @string + " / 18" + " at time: " + time() as @string)

    wait(2)
}